module VerifyExamples.Compiler exposing (compile)

import Regex exposing (HowMany(..), regex)
import String
import String.Extra
import VerifyExamples.Function exposing (Function)
import VerifyExamples.Test exposing (Test)
import VerifyExamples.TestSuite exposing (TestSuite)


type alias Info =
    { imports : List String
    , types : List String
    , functionToTest : Maybe String
    , helperFunctions : List Function
    , moduleName : String
    , testName : String
    }


compile : String -> TestSuite -> List ( String, String )
compile moduleName suite =
    suite.tests
        |> List.indexedMap
            (\index ->
                compileTest
                    { imports = suite.imports
                    , types = suite.types
                    , functionToTest = suite.functionToTest
                    , helperFunctions = suite.helperFunctions
                    , moduleName = moduleName
                    , testName = testName moduleName suite.functionToTest index
                    }
                    index
            )


testName : String -> Maybe String -> Int -> String
testName moduleName functionToTest index =
    moduleName
        ++ ".Function_"
        ++ Maybe.withDefault "" functionToTest
        ++ "_Example"
        ++ toString index


compileTest : Info -> Int -> Test -> ( String, String )
compileTest info index test =
    ( info.testName
    , String.join "\n" <|
        List.concat
            [ moduleHeader info
            , spec info test
            ]
    )


moduleHeader : Info -> List String
moduleHeader { moduleName, testName, imports, types, helperFunctions } =
    [ "module Doc." ++ testName ++ "Spec exposing (spec)"
    , ""
    , "-- This file got generated by [elm-verify-examples](https://github.com/stoeffel/elm-verify-examples)."
    , "-- Please don't modify this file by hand!"
    , ""
    , "import Test"
    , "import Expect"
    , "import " ++ moduleName ++ " exposing(..)"
    , ""
    ]
        ++ imports
        ++ [ "" ]
        ++ types
        ++ [ "" ]
        ++ (helperFunctions
                |> List.filter .isUsed
                |> List.map .value
           )


spec : Info -> Test -> List String
spec info test =
    [ ""
    , ""
    , "spec : Test.Test"
    , "spec ="
    ]
        ++ List.map (indent 1) (toTest info.testName test)


toTest : String -> Test -> List String
toTest name test =
    [ "Test.test \""
        ++ "Example: "
        ++ name
        ++ " -- "
        ++ exampleName test
        ++ "\" <|"
    , indent 1 "\\() ->"
    , indent 2 "Expect.equal"
    , indent 3 "("
    ]
        ++ (List.map (indent 4) <| String.lines test.assertion)
        ++ [ indent 3 ")"
           , indent 3 "("
           ]
        ++ (List.map (indent 4) <| String.lines test.expectation)
        ++ [ indent 3 ")"
           ]


exampleName : Test -> String
exampleName test =
    (test.assertion ++ " --> " ++ test.expectation)
        |> String.Extra.replace "\n" " "
        |> String.Extra.clean
        |> String.Extra.ellipsis 40
        |> String.Extra.surround "`"
        |> escape


indent : Int -> String -> String
indent count str =
    List.repeat (count * 4) " "
        ++ [ str ]
        |> String.join ""


escape : String -> String
escape =
    Regex.replace All (regex "\\\\") (\_ -> "\\\\")
        >> Regex.replace All (regex "\\\"") (\_ -> "\\\"")
        >> Regex.replace All (regex "\\s\\s+") (\_ -> " ")
